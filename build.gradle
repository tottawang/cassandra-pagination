buildscript {
	ext {
		springBootVersion = '1.3.2.RELEASE'
		jerseyVersion = '2.23.1'
		restAssuredVersion = '2.5.0'
		jbehaveVersion = '4.0.4'
		assertjVersion = '2.2.0'
		jsonPath = '2.1.0'
		gitPropertiesVersion = '1.4.7'
		jerseySecurityVersion = '2.17'
		guavaVersion = '19.0'
		springContextSupport = '4.2.6.RELEASE'
		// https://groups.google.com/a/lists.datastax.com/forum/#!topic/java-driver-user/bo-Uhr2g_s4
		// https://datastax-oss.atlassian.net/browse/JAVA-1310
		// On 3.0.1 mapper started mapping getter methods as well as fields which is not proper change
		// hence we keep using 3.0.7 to avoid the noise, and wait a better and complete solution on 3.0.2
		cassandraDriver='3.0.7'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gitPropertiesVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'eclipse'
apply plugin: 'io.spring.dependency-management' 
apply plugin: 'com.gorylenko.gradle-git-properties'

jar {
	baseName = 'sample'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'    
    resolutionStrategy.dependencySubstitution {
    	// Substitute one module dependency for another
    	//jersey 2.22.1 is being replaced by 2.23.1 a we want to use LoggingFeature 
    	substitute module("org.glassfish.jersey.containers:jersey-container-servlet-core:2.22.1") with module("org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}")
    	substitute module("org.glassfish.jersey.containers:jersey-container-servlet:2.22.1") with module("org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}")
  } 
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
	compile("org.springframework.boot:spring-boot-starter-integration:${springBootVersion}")
	compile("org.springframework.boot:spring-boot-starter-jersey:${springBootVersion}")
	compile("com.datastax.cassandra:cassandra-driver-core:${cassandraDriver}")
    compile("com.datastax.cassandra:cassandra-driver-mapping:${cassandraDriver}")
    compile("com.datastax.cassandra:cassandra-driver-extras:${cassandraDriver}")
	compile("org.apache.httpcomponents:httpclient:4.5.2")
	compile("org.apache.httpcomponents:httpcore:4.4.4")
	compile("com.jayway.jsonpath:json-path:${jsonPath}")
	compile("com.google.guava:guava:${guavaVersion}")
	compile("org.springframework:spring-context-support:${springContextSupport}")
	compile group: 'org.springframework.cloud', name: 'spring-cloud-aws-messaging', version: '1.1.3.RELEASE'
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
	systemProperties = System.properties
}

eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.10'
}
